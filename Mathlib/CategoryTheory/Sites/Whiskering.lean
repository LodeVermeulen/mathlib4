/-
Copyright (c) 2021 Adam Topaz. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Adam Topaz
-/
import Mathlib.CategoryTheory.Sites.LeftExact
import Mathlib.CategoryTheory.Sites.Sheafification

#align_import category_theory.sites.whiskering from "leanprover-community/mathlib"@"9f9015c645d85695581237cc761981036be8bd37"

/-!

In this file we construct the functor `Sheaf J A ‚•§ Sheaf J B` between sheaf categories
obtained by composition with a functor `F : A ‚•§ B`.

In order for the sheaf condition to be preserved, `F` must preserve the correct limits.
The lemma `Presheaf.IsSheaf.comp` says that composition with such an `F` indeed preserves the
sheaf condition.

The functor between sheaf categories is called `sheafCompose J F`.
Given a natural transformation `Œ∑ : F ‚ü∂ G`, we obtain a natural transformation
`sheafCompose J F ‚ü∂ sheafCompose J G`, which we call `sheafCompose_map J Œ∑`.

Under the assumptions `HasWeakSheafify J A`, `HasWeakSheafify J B`, we define
a type class `PreservesSheafification J F` which expresses that sheafification
commutes with the postcomposition with `F`.

-/


namespace CategoryTheory

open CategoryTheory Category Limits

universe v‚ÇÅ v‚ÇÇ v‚ÇÉ u‚ÇÅ u‚ÇÇ u‚ÇÉ

variable {C : Type u‚ÇÅ} [Category.{v‚ÇÅ} C]
variable {A : Type u‚ÇÇ} [Category.{v‚ÇÇ} A]
variable {B : Type u‚ÇÉ} [Category.{v‚ÇÉ} B]
variable (J : GrothendieckTopology C)
variable {U : C} (R : Presieve U)
variable (F G H : A ‚•§ B) (Œ∑ : F ‚ü∂ G) (Œ≥ : G ‚ü∂ H)

/-- Describes the property of a functor to "preserve sheaves". -/
class GrothendieckTopology.HasSheafCompose : Prop where
  /-- For every sheaf `P`, `P ‚ãô F` is a sheaf. -/
  isSheaf (P : C·µí·µñ ‚•§ A) (hP : Presheaf.IsSheaf J P) : Presheaf.IsSheaf J (P ‚ãô F)

variable [J.HasSheafCompose F] [J.HasSheafCompose G] [J.HasSheafCompose H]

/-- Composing a functor which `HasSheafCompose`, yields a functor between sheaf categories. -/
@[simps]
def sheafCompose : Sheaf J A ‚•§ Sheaf J B where
  obj G := ‚ü®G.val ‚ãô F, GrothendieckTopology.HasSheafCompose.isSheaf G.val G.2‚ü©
  map Œ∑ := ‚ü®whiskerRight Œ∑.val _‚ü©
  map_id _ := Sheaf.Hom.ext _ _ <| whiskerRight_id _
  map_comp _ _ := Sheaf.Hom.ext _ _ <| whiskerRight_comp _ _ _
set_option linter.uppercaseLean3 false in
#align category_theory.Sheaf_compose CategoryTheory.sheafCompose

variable {F G}

/--
If `Œ∑ : F ‚ü∂ G` is a natural transformation then we obtain a morphism of functors
`sheafCompose J F ‚ü∂ sheafCompose J G` by whiskering with `Œ∑` on the level of presheaves.
-/
def sheafCompose_map : sheafCompose J F ‚ü∂ sheafCompose J G where
  app := fun X => .mk <| whiskerLeft _ Œ∑

@[simp]
lemma sheafCompose_id : sheafCompose_map (F := F) J (ùüô _) = ùüô _ := rfl

@[simp]
lemma sheafCompose_comp :
    sheafCompose_map J (Œ∑ ‚â´ Œ≥) = sheafCompose_map J Œ∑ ‚â´ sheafCompose_map J Œ≥ := rfl

namespace GrothendieckTopology.Cover

variable (F G) {J}
variable (P : C·µí·µñ ‚•§ A) {X : C} (S : J.Cover X)

/-- The multicospan associated to a cover `S : J.Cover X` and a presheaf of the form `P ‚ãô F`
is isomorphic to the composition of the multicospan associated to `S` and `P`,
composed with `F`. -/
def multicospanComp : (S.index (P ‚ãô F)).multicospan ‚âÖ (S.index P).multicospan ‚ãô F :=
  NatIso.ofComponents
    (fun t =>
      match t with
      | WalkingMulticospan.left a => eqToIso rfl
      | WalkingMulticospan.right b => eqToIso rfl)
    (by
      rintro (a | b) (a | b) (f | f | f)
      all_goals aesop_cat)
#align category_theory.grothendieck_topology.cover.multicospan_comp CategoryTheory.GrothendieckTopology.Cover.multicospanComp

@[simp]
theorem multicospanComp_app_left (a) :
    (S.multicospanComp F P).app (WalkingMulticospan.left a) = eqToIso rfl :=
  rfl
#align category_theory.grothendieck_topology.cover.multicospan_comp_app_left CategoryTheory.GrothendieckTopology.Cover.multicospanComp_app_left

@[simp]
theorem multicospanComp_app_right (b) :
    (S.multicospanComp F P).app (WalkingMulticospan.right b) = eqToIso rfl :=
  rfl
#align category_theory.grothendieck_topology.cover.multicospan_comp_app_right CategoryTheory.GrothendieckTopology.Cover.multicospanComp_app_right

@[simp]
theorem multicospanComp_hom_app_left (a) :
    (S.multicospanComp F P).hom.app (WalkingMulticospan.left a) = eqToHom rfl :=
  rfl
#align category_theory.grothendieck_topology.cover.multicospan_comp_hom_app_left CategoryTheory.GrothendieckTopology.Cover.multicospanComp_hom_app_left

@[simp]
theorem multicospanComp_hom_app_right (b) :
    (S.multicospanComp F P).hom.app (WalkingMulticospan.right b) = eqToHom rfl :=
  rfl
#align category_theory.grothendieck_topology.cover.multicospan_comp_hom_app_right CategoryTheory.GrothendieckTopology.Cover.multicospanComp_hom_app_right

@[simp]
theorem multicospanComp_hom_inv_left (P : C·µí·µñ ‚•§ A) {X : C} (S : J.Cover X) (a) :
    (S.multicospanComp F P).inv.app (WalkingMulticospan.left a) = eqToHom rfl :=
  rfl
#align category_theory.grothendieck_topology.cover.multicospan_comp_hom_inv_left CategoryTheory.GrothendieckTopology.Cover.multicospanComp_hom_inv_left

@[simp]
theorem multicospanComp_hom_inv_right (P : C·µí·µñ ‚•§ A) {X : C} (S : J.Cover X) (b) :
    (S.multicospanComp F P).inv.app (WalkingMulticospan.right b) = eqToHom rfl :=
  rfl
#align category_theory.grothendieck_topology.cover.multicospan_comp_hom_inv_right CategoryTheory.GrothendieckTopology.Cover.multicospanComp_hom_inv_right

/-- Mapping the multifork associated to a cover `S : J.Cover X` and a presheaf `P` with
respect to a functor `F` is isomorphic (upto a natural isomorphism of the underlying functors)
to the multifork associated to `S` and `P ‚ãô F`. -/
def mapMultifork :
    F.mapCone (S.multifork P) ‚âÖ
      (Limits.Cones.postcompose (S.multicospanComp F P).hom).obj (S.multifork (P ‚ãô F)) :=
  Cones.ext (eqToIso rfl)
    (by
      rintro (a | b)
      ¬∑ dsimp
        erw [Category.id_comp, multicospanComp_hom_app_left, eqToHom_refl, Category.comp_id]
      ¬∑ dsimp
        erw [Functor.map_comp, Category.assoc, Category.id_comp,
          multicospanComp_hom_app_right, eqToHom_refl, Category.comp_id]
        rfl)
#align category_theory.grothendieck_topology.cover.map_multifork CategoryTheory.GrothendieckTopology.Cover.mapMultifork

end GrothendieckTopology.Cover

/--
Composing a sheaf with a functor preserving the limit of `(S.index P).multicospan` yields a functor
between sheaf categories.
-/
instance hasSheafCompose_of_preservesMulticospan (F : A ‚•§ B)
    [‚àÄ (X : C) (S : J.Cover X) (P : C·µí·µñ ‚•§ A), PreservesLimit (S.index P).multicospan F] :
    J.HasSheafCompose F where
  isSheaf P hP := by
    rw [Presheaf.isSheaf_iff_multifork] at hP ‚ä¢
    intro X S
    obtain ‚ü®h‚ü© := hP X S
    replace h := isLimitOfPreserves F h
    replace h := Limits.IsLimit.ofIsoLimit h (S.mapMultifork F P)
    exact ‚ü®Limits.IsLimit.postcomposeHomEquiv (S.multicospanComp F P) _ h‚ü©

/--
Composing a sheaf with a functor preserving limits of the same size as the hom sets in `C`¬†yields a
functor between sheaf categories.

Note: the size of the limit that `F`¬†is required to preserve in
`hasSheafCompose_of_preservesMulticospan` is in general larger than this.
-/
instance hasSheafCompose_of_preservesLimitsOfSize [PreservesLimitsOfSize.{v‚ÇÅ, max u‚ÇÅ v‚ÇÅ} F] :
    J.HasSheafCompose F where
  isSheaf _ hP := Presheaf.isSheaf_comp_of_isSheaf J _ F hP

variable (F : A ‚•§ B) [HasWeakSheafify J A] [HasWeakSheafify J B] [J.HasSheafCompose F]
  (P : C·µí·µñ ‚•§ A)

/-- Given a Grothendieck topology `J` on `C`, and `F : A ‚•§ B`, this is the natural
transformation defined for any presheaf `P : C·µí·µñ ‚•§ A`, from the associated sheaf of `P ‚ãô B`
to the postcomposition with `F` of the associated sheaf of `P`. -/
noncomputable def presheafToSheafCompose :
    (whiskeringRight C·µí·µñ A B).obj F ‚ãô presheafToSheaf J B ‚ü∂
      presheafToSheaf J A ‚ãô sheafCompose J F where
  app P := ((sheafificationAdjunction J B).homEquiv _ _).symm (whiskerRight (toSheafify J P) F)
  naturality {P Q} f := by
    dsimp
    erw [‚Üê Adjunction.homEquiv_naturality_left_symm,
      ‚Üê Adjunction.homEquiv_naturality_right_symm]
    dsimp
    rw [‚Üê whiskerRight_comp, ‚Üê whiskerRight_comp, toSheafify_naturality]

/-- The canonical map `sheafify J (P ‚ãô F) ‚ü∂ sheafify J P ‚ãô F`. -/
noncomputable def sheafifyCompose :
    sheafify J (P ‚ãô F) ‚ü∂ sheafify J P ‚ãô F :=
  (sheafToPresheaf J B).map ((presheafToSheafCompose J F).app P)

@[reassoc (attr := simp)]
lemma sheafifyCompose_fac :
    toSheafify J (P ‚ãô F) ‚â´ sheafifyCompose J F P = whiskerRight (toSheafify J P) F := by
  dsimp only [sheafifyCompose, toSheafify, presheafToSheafCompose]
  erw [Adjunction.homEquiv_counit, Adjunction.unit_naturality_assoc]
  simp

/-- Given a Grothendieck topology `J` on `C` and `F : A ‚•§ B`, this is the condition
that sheafification for `J` commutes with the postcomposition with `F`. -/
class PreservesSheafification : Prop where
  isIso : IsIso (presheafToSheafCompose J F) := by infer_instance

lemma PreservesSheafification.mk' (h : ‚àÄ (P : C·µí·µñ ‚•§ A), IsIso (sheafifyCompose J F P)) :
    PreservesSheafification J F where
  isIso := by
    have : ‚àÄ P, IsIso ((sheafToPresheaf J B).map ((presheafToSheafCompose J F).app P)) := h
    have : ‚àÄ P, IsIso ((presheafToSheafCompose J F).app P) :=
      fun p => isIso_of_reflects_iso _ (sheafToPresheaf J B)
    apply NatIso.isIso_of_isIso_app

variable [PreservesSheafification J F]

attribute [instance] PreservesSheafification.isIso

/-- Given a Grothendieck topology `J` on `C` and `F : A ‚•§ B`
such that `[PreservesSheafification J F]`, this is the condition
that sheafification for `J` commutes with the postcomposition with `F`. -/
noncomputable def presheafToSheafComposeIso :
    (whiskeringRight C·µí·µñ A B).obj F ‚ãô presheafToSheaf J B ‚âÖ
      presheafToSheaf J A ‚ãô sheafCompose J F :=
  asIso (presheafToSheafCompose J F)

/-- The canonical isomorphism `sheafify J (P ‚ãô F) ‚âÖ sheafify J P ‚ãô F` when
`F` preserves the sheafification. -/
noncomputable def sheafifyComposeIso :
    sheafify J (P ‚ãô F) ‚âÖ sheafify J P ‚ãô F :=
  (sheafToPresheaf J B).mapIso ((presheafToSheafComposeIso J F).app P)

@[simp]
lemma sheafifyComposeIso_hom :
    (sheafifyComposeIso J F P).hom = sheafifyCompose J F P := rfl

@[reassoc (attr := simp)]
lemma sheafifyComposeIso_hom_inv_id :
    sheafifyCompose J F P ‚â´ (sheafifyComposeIso J F P).inv = ùüô _ :=
  (sheafifyComposeIso J F P).hom_inv_id

@[reassoc (attr := simp)]
lemma sheafifyComposeIso_inv_hom_id :
    (sheafifyComposeIso J F P).inv ‚â´ sheafifyCompose J F P = ùüô _ :=
  (sheafifyComposeIso J F P).inv_hom_id

instance : IsIso (sheafifyCompose J F P) :=
  (inferInstance : IsIso (sheafifyComposeIso J F P).hom)

@[reassoc (attr := simp)]
lemma sheafifyComposeIso_inv_fac :
    whiskerRight (toSheafify J P) F ‚â´ (sheafifyComposeIso J F P).inv =
      toSheafify J (P ‚ãô F) := by
  rw [‚Üê cancel_mono (sheafifyCompose J F P), assoc, sheafifyComposeIso_inv_hom_id,
    comp_id, sheafifyCompose_fac]

namespace GrothendieckTopology

variable {D : Type*} [Category.{max v‚ÇÅ u‚ÇÅ} D]
  [ConcreteCategory.{max v‚ÇÅ u‚ÇÅ} D] [PreservesLimits (forget D)]
  [‚àÄ (P : C·µí·µñ ‚•§ D) (X : C) (S : J.Cover X), HasMultiequalizer (S.index P)]
  [‚àÄ X : C, HasColimitsOfShape (J.Cover X)·µí·µñ D]
  [‚àÄ X : C, PreservesColimitsOfShape (J.Cover X)·µí·µñ (forget D)]
  [(forget D).ReflectsIsomorphisms]

/-- The canonical isomorphism `J.plusObj (P ‚ãô forget D) ‚âÖ J.plusObj P ‚ãô forget D`. -/
def plusObjComposeForget (P : C·µí·µñ ‚•§ D) :
    J.plusObj (P ‚ãô forget D) ‚âÖ J.plusObj P ‚ãô forget D := by
  sorry

@[reassoc (attr := simp)]
lemma plusObjComposeForget_hom_naturality {P Q : C·µí·µñ ‚•§ D} (œÜ : P ‚ü∂ Q) :
    J.plusMap (whiskerRight œÜ (forget D)) ‚â´ (J.plusObjComposeForget Q).hom =
    (J.plusObjComposeForget P).hom ‚â´
      whiskerRight (J.plusMap œÜ) (forget D) := sorry

@[reassoc (attr := simp)]
lemma plusObjComposeForget_hom_fac (P : C·µí·µñ ‚•§ D) :
    J.toPlus (P ‚ãô forget D) ‚â´ (J.plusObjComposeForget P).hom =
      whiskerRight (J.toPlus P) (forget D) := by
  sorry

/-- The canonical isomorphism `J.sheafify (P ‚ãô forget D) ‚âÖ J.sheafify P ‚ãô forget D`. -/
noncomputable def sheafifyComposeForgetIso (P : C·µí·µñ ‚•§ D) :
    J.sheafify (P ‚ãô forget D) ‚âÖ J.sheafify P ‚ãô forget D :=
  (J.plusFunctor _).mapIso (J.plusObjComposeForget P) ‚â™‚â´
    J.plusObjComposeForget (plusObj J P)

@[reassoc (attr := simp)]
lemma sheafifyComposeForget_fac (P : C·µí·µñ ‚•§ D) :
    J.toSheafify (P ‚ãô forget D) ‚â´ (J.sheafifyComposeForgetIso P).hom =
      whiskerRight (J.toSheafify P) (forget D) := by
  dsimp [toSheafify, sheafifyComposeForgetIso]
  rw [assoc, ‚Üê plusMap_comp_assoc, plusObjComposeForget_hom_fac,
    plusObjComposeForget_hom_naturality, plusMap_toPlus,
    plusObjComposeForget_hom_fac_assoc, toPlus_naturality,
    plusMap_toPlus, whiskerRight_comp]

@[reassoc]
lemma sheafifyCompose_forget_eq (P : C·µí·µñ ‚•§ D) :
    sheafifyCompose J (forget D) P =
      (plusPlusIsoSheafify J _ (P ‚ãô forget D)).inv ‚â´
        (J.sheafifyComposeForgetIso P).hom ‚â´
          whiskerRight (plusPlusIsoSheafify J _ P).hom (forget D) := by
  sorry

instance : PreservesSheafification J (forget D) :=
  PreservesSheafification.mk' _ _ (fun P => by
    rw [J.sheafifyCompose_forget_eq]
    infer_instance)

end GrothendieckTopology

end CategoryTheory
