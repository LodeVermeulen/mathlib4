/-
Copyright (c) 2024 Christian Merten. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Christian Merten
-/
import Mathlib.CategoryTheory.CofilteredSystem
import Mathlib.CategoryTheory.Galois.Decomposition
import Mathlib.CategoryTheory.Limits.FunctorCategory
import Mathlib.Algebra.Category.GroupCat.Limits
import Mathlib.ProCoyoneda

/-!
# Pro-Representability of fiber functors

We show that any fiber functor is pro-representable, i.e. there exists a pro-object
`X : I ‚•§ C` such that `F` is naturally isomorphic to `X ‚ãô coyoneda`.

## Main results


## References

* H. W. Lenstra. Galois theory for schemes
  <https://websites.math.leidenuniv.nl/algebra/GSchemes.pdf>

-/

universe u‚ÇÅ u‚ÇÇ w

section

variable {G M : Type*} [Group G] [Monoid M] (f : G ‚âÉ* M) (g : M ‚âÉ* G)

--def equivUnits (h : ‚àÄ (x : M), IsUnit x) : M ‚âÉ* MÀ£ where
--  toFun m := by
--    admit

end

namespace CategoryTheory

namespace PreGaloisCategory

open Limits Functor

variable (C : Type u‚ÇÅ) [Category.{u‚ÇÇ} C] [GaloisCategory C]

def GaloisObjects : Set C := { A : C | IsGalois A }

instance (A : GaloisObjects C) : IsGalois A.val :=
  A.2

variable (F : C ‚•§ FintypeCat.{u‚ÇÇ}) [FiberFunctor F]

variable {C}

def Idx : Type (max u‚ÇÅ u‚ÇÇ) := (A : GaloisObjects C) √ó F.obj (A : C)

instance : Category.{u‚ÇÇ} (Idx F) where
  Hom := by
    intro ‚ü®A, a‚ü© ‚ü®B, b‚ü©
    exact { f : (B : C) ‚ü∂ A // F.map f b = a }
  id := by
    intro ‚ü®A, a‚ü©
    exact ‚ü®ùüô (A : C), by simp‚ü©
  comp := by
    intro ‚ü®A, a‚ü© ‚ü®B, b‚ü© ‚ü®C, c‚ü© ‚ü®f, hf‚ü© ‚ü®g, hg‚ü©
    have h : F.map (g ‚â´ f) c = a := by
      simp only [F.map_comp, FintypeCat.comp_apply, hf, hg]
    exact ‚ü®g ‚â´ f, h‚ü©

instance : IsFilteredOrEmpty (Idx F) where
  cocone_objs := fun ‚ü®A, a‚ü© ‚ü®B, b‚ü© ‚Ü¶ by
    obtain ‚ü®Z, f, z, hgal, hfz‚ü© := exists_hom_from_galois_of_fiber F (A ‚®Ø B)
      <| (fiberBinaryProductEquiv F A B).symm (a, b)
    refine ‚ü®‚ü®‚ü®Z, hgal‚ü©, z‚ü©, ‚ü®f ‚â´ prod.fst, ?_‚ü©, ‚ü®f ‚â´ prod.snd, ?_‚ü©, trivial‚ü©
    simp only [F.map_comp, hfz, FintypeCat.comp_apply, fiberBinaryProductEquiv_symm_fst_apply]
    simp only [F.map_comp, hfz, FintypeCat.comp_apply, fiberBinaryProductEquiv_symm_snd_apply]
  cocone_maps := fun ‚ü®A, a‚ü© ‚ü®B, b‚ü© ‚ü®f, hf‚ü© ‚ü®g, hg‚ü© ‚Ü¶ by
    obtain ‚ü®Z, h, z, hgal, hhz‚ü© := exists_hom_from_galois_of_fiber F B b
    refine ‚ü®‚ü®‚ü®Z, hgal‚ü©, z‚ü©, ‚ü®h, hhz‚ü©, ?_‚ü©
    apply Subtype.ext
    apply evaluationInjective_of_isConnected F Z A z
    show F.map (h ‚â´ f) z = F.map (h ‚â´ g) z
    simp only [map_comp, FintypeCat.comp_apply, hhz, hf, hg]

def can : (Idx F)·µí·µñ ‚•§ C where
  obj := fun ‚ü®‚ü®A, _‚ü©‚ü© ‚Ü¶ A
  map := fun ‚ü®f, _‚ü© ‚Ü¶ f

@[simp]
lemma can_obj (A : (Idx F)·µí·µñ) : (can F).obj A = A.unop.1 :=
  rfl

@[simp]
lemma can_map_eq {A B : (Idx F)·µí·µñ} (f : A ‚ü∂ B) : ((can F).map f) = f.unop.val :=
  rfl

def cocone : Cocone ((can F).rightOp ‚ãô coyoneda) where
  pt := F ‚ãô FintypeCat.incl
  Œπ := {
    app := by
      intro ‚ü®A, a‚ü©
      exact {
        app := by
          intro X (f : (A : C) ‚ü∂ X)
          -- evaluation at a
          exact F.map f a
      }
    naturality := by
      intro ‚ü®A, a‚ü© ‚ü®B, b‚ü© ‚ü®f, hf‚ü©
      ext Y (g : (A : C) ‚ü∂ Y)
      simp [hf]
  }

@[simp]
lemma cocone_app (A : Idx F) (B : C) (f : (A.1 : C) ‚ü∂ B) :
    ((cocone F).Œπ.app A).app B f = F.map f A.2 :=
  rfl

universe v u

noncomputable def iscolimit : IsColimit (cocone F) := by
  apply evaluationJointlyReflectsColimits
  intro X
  let G : Idx F ‚•§ Type u‚ÇÇ := (((can F).rightOp ‚ãô coyoneda) ‚ãô (evaluation C (Type u‚ÇÇ)).obj X)
  let s : Cocone G := ((evaluation C (Type u‚ÇÇ)).obj X).mapCocone (cocone F)
  show IsColimit s
  refine Types.FilteredColimit.isColimitOf G s ?_ ?_
  intro (x : F.obj X)
  obtain ‚ü®Y, i, y, h1, _, _‚ü© := fiber_in_connected_component F X x
  obtain ‚ü®Z, f, z, hgal, hfz‚ü© := exists_hom_from_galois_of_fiber F Y y
  use ‚ü®‚ü®Z, hgal‚ü©, z‚ü©
  use f ‚â´ i
  show x = F.map (f ‚â´ i) z
  simp only [‚Üêh1, map_comp, FintypeCat.comp_apply, hfz]
  intro ‚ü®A, a‚ü© ‚ü®B, b‚ü© (u : (A : C) ‚ü∂ X) --(v : (B : C) ‚ü∂ X) (h : F.map u a = F.map v b)
  intro (v : (B : C) ‚ü∂ X) (h : F.map u a = F.map v b)
  obtain ‚ü®‚ü®‚ü®Z, (hgal : IsGalois Z)‚ü©, z‚ü©, ‚ü®f, hf‚ü©, ‚ü®g, hg‚ü©, _‚ü© :=
    @IsFilteredOrEmpty.cocone_objs (Idx F) _ _ (‚ü®A, a‚ü© : Idx F) (‚ü®B, b‚ü© : Idx F)
  use ‚ü®‚ü®Z, hgal‚ü©, z‚ü©
  use ‚ü®f, hf‚ü©
  use ‚ü®g, hg‚ü©
  have : IsGalois Z := hgal
  have : IsConnected Z := inferInstance
  apply evaluationInjective_of_isConnected F Z X z
  show F.map (f ‚â´ u) z = F.map (g ‚â´ v) z
  rw [map_comp, FintypeCat.comp_apply, hf, map_comp, FintypeCat.comp_apply, hg, h]

--instance (X : C) : SMul (Aut F) (F.obj X) := ‚ü®fun œÉ a => (œÉ.app X).hom a‚ü©

--private noncomputable def autMap' {A B : C} [IsGalois B] (f : A ‚ü∂ B) (a : F.obj A)
--    (b : F.obj B) (œÉ : Aut A) : { œÑ : Aut B | F.map œÑ.hom b = F.map (œÉ.hom ‚â´ f) a } := by
--  choose œÑ h using MulAction.surjective_smul (Aut B) b (F.map (œÉ.hom ‚â´ f) a)
--  exact ‚ü®œÑ, h‚ü©
--
--private noncomputable def autMap {A B : C} [IsGalois B] (f : A ‚ü∂ B) (a : F.obj A)
--    (b : F.obj B) (œÉ : Aut A) : Aut B := autMap' F f a b œÉ

noncomputable def autMap {A B : C} [IsGalois B] (f : A ‚ü∂ B) (a : F.obj A)
    (b : F.obj B) (œÉ : Aut A) : Aut B :=
  (evaluationEquivOfIsGalois F B b).symm (F.map (œÉ.hom ‚â´ f) a)

@[simp]
lemma autMap_eval {A B : C} [IsGalois B] (f : A ‚ü∂ B) (a : F.obj A) (b : F.obj B)
    (œÉ : Aut A) : F.map (autMap F f a b œÉ : Aut B).hom b = F.map (œÉ.hom ‚â´ f) a := by
  simp [autMap]

lemma autMap_surjective {A B : C} [IsGalois A] [IsGalois B] (f : A ‚ü∂ B)
    (a : F.obj A) (b : F.obj B) : Function.Surjective (autMap F f a b) := by
  intro œÉ
  --have h : Nonempty (F.obj A) := nonempty_fibre_of_connected A
  obtain ‚ü®a', ha'‚ü© := surjective_of_nonempty_fiber_of_isConnected F f (F.map œÉ.hom b)
  obtain ‚ü®œÑ, (hœÑ : F.map œÑ.hom a = a')‚ü© := MulAction.exists_smul_eq (Aut A) a a'
  use œÑ
  apply evaluation_aut_injective_of_isConnected F B b
  simp only [autMap_eval, map_comp, FintypeCat.comp_apply]
  rw [hœÑ, ha']

lemma autMap_comp {A B : C} [IsConnected A] [IsGalois B] (f : A ‚ü∂ B) (a : F.obj A) (b : F.obj B)
    (h : F.map f a = b)
    (œÉ : Aut A) : œÉ.hom ‚â´ f = f ‚â´ (autMap F f a b œÉ).hom := by
  apply evaluationInjective_of_isConnected F A B a
  show F.map (œÉ.hom ‚â´ f) a = F.map (f ‚â´ (autMap F f a b œÉ).hom) a
  simp only [map_comp, FintypeCat.comp_apply, h, autMap_eval]

lemma autMap_mul {A B : C} [IsConnected A] [IsGalois B] (f : A ‚ü∂ B) (a : F.obj A) (b : F.obj B)
    (h : F.map f a = b)
    (œÉ œÑ : Aut A) : autMap F f a b (œÉ * œÑ) = autMap F f a b œÉ * autMap F f a b œÑ := by
  apply evaluation_aut_injective_of_isConnected F (B : C) b
  show F.map (autMap F f a b (œÉ * œÑ)).hom b =
    F.map (autMap F f a b œÉ * autMap F f a b œÑ).hom b
  simp only [autMap_eval]
  convert_to F.map ((œÑ.hom ‚â´ œÉ.hom) ‚â´ f) a
    = F.map ((f ‚â´ (autMap F f a b œÑ).hom) ‚â´ (autMap F f a b œÉ).hom) a
  erw [‚Üê h, Functor.map_comp]
  simp only [FintypeCat.comp_apply, autMap_eval, map_comp, Category.assoc]
  erw [‚Üê autMap_comp F f a b h œÑ, Category.assoc, Category.assoc,
    ‚Üê autMap_comp F f a b h œÉ]

noncomputable def autMapMul : {A B : (Idx F)·µí·µñ} ‚Üí (A ‚ü∂ B) ‚Üí Aut (A.1.1 : C) ‚Üí* Aut (B.1.1 : C) := by
  intro ‚ü®‚ü®A, (h1 : IsGalois A)‚ü©, a‚ü© ‚ü®‚ü®B, (h2 : IsGalois B)‚ü©, b‚ü© ‚ü®f, hf‚ü©
  apply MonoidHom.mk'
  exact autMap_mul F f a b hf

noncomputable def autGaloisSystem : (Idx F)·µí·µñ ‚•§ GroupCat.{u‚ÇÇ} where
  obj := fun ‚ü®A, _‚ü© => GroupCat.of <| Aut (A : C)
  map := by
    intro ‚ü®A, _‚ü© ‚ü®B, _‚ü© f
    exact (autMapMul F f : Aut (A : C) ‚Üí* Aut (B : C))
  map_id := by
    intro ‚ü®‚ü®A, (hAgal : IsGalois A)‚ü©, a‚ü©
    ext (œÉ : Aut A)
    show autMap F (ùüô A) a a œÉ = œÉ
    apply evaluation_aut_injective_of_isConnected F (A : C) a
    simp only [autMap_eval F (ùüô A) a a œÉ, Category.comp_id]
  map_comp := by
    intro ‚ü®‚ü®A, (hAgal : IsGalois A)‚ü©, a‚ü© ‚ü®‚ü®B, (hBgal : IsGalois B)‚ü©, b‚ü©
      ‚ü®‚ü®C, (hCgal : IsGalois C)‚ü©, c‚ü© ‚ü®f, hf‚ü© ‚ü®g, hg‚ü©
    ext (œÉ : Aut A)
    show autMap F (f ‚â´ g) a c œÉ = autMap F g b c (autMap F f a b œÉ)
    apply evaluation_aut_injective_of_isConnected F C c
    simp only [autMap_eval, map_comp, FintypeCat.comp_apply]

def MonCat.uliftFunctor : MonCat.{u‚ÇÅ} ‚•§ MonCat.{max u‚ÇÅ u‚ÇÇ} where
  obj X := MonCat.of (ULift.{max u‚ÇÅ u‚ÇÇ, u‚ÇÅ} X)
  map {X Y} f := MonCat.ofHom <|
    MulEquiv.ulift.symm.toMonoidHom.comp <| f.comp MulEquiv.ulift.toMonoidHom
  map_id X := by rfl
  map_comp {X Y Z} f g := by rfl

def GroupCat.uliftFunctor : GroupCat.{u‚ÇÅ} ‚•§ GroupCat.{max u‚ÇÅ u‚ÇÇ} where
  obj X := GroupCat.of (ULift.{u‚ÇÇ, u‚ÇÅ} X)
  map {X Y} f := GroupCat.ofHom <|
    MulEquiv.ulift.symm.toMonoidHom.comp <| f.comp MulEquiv.ulift.toMonoidHom
  map_id X := by rfl
  map_comp {X Y Z} f g := by rfl

noncomputable example [UnivLE.{max u‚ÇÅ u‚ÇÇ, u‚ÇÇ}] : GroupCat.{u‚ÇÇ} := limit (autGaloisSystem F)

noncomputable def autGaloisSystem' : (Idx F)·µí·µñ ‚•§ GroupCat.{max u‚ÇÅ u‚ÇÇ} :=
  autGaloisSystem F ‚ãô GroupCat.uliftFunctor.{u‚ÇÇ, u‚ÇÅ}

@[simp]
theorem autGaloisSystem'_map {A B : (Idx F)·µí·µñ} (f : A ‚ü∂ B) (œÜ : Aut (A.unop.1 : C)) :
    ((autGaloisSystem' F).map f) ‚ü®œÜ‚ü© = ‚ü®autMapMul F f œÜ‚ü© :=
  rfl

noncomputable def autGalois : GroupCat.{max u‚ÇÅ u‚ÇÇ} := limit (autGaloisSystem' F)

noncomputable def autGalois.œÄ (A : Idx F) : autGalois F ‚Üí* Aut (A.1 : C) :=
  MonoidHom.comp MulEquiv.ulift.toMonoidHom (limit.œÄ (autGaloisSystem' F) (Opposite.op A))

theorem autGalois.œÄ_apply (A : Idx F) (x : autGalois F) :
    autGalois.œÄ F A x = Equiv.ulift (limit.œÄ (autGaloisSystem' F) (Opposite.op A) x) :=
  rfl

lemma autGalois_ext (f g : autGalois F)
    (h : ‚àÄ (A : (Idx F)·µí·µñ), autGalois.œÄ F A.unop f = autGalois.œÄ F A.unop g) : f = g := by
  apply Concrete.limit_ext (autGaloisSystem' F) f g
  intro ‚ü®A‚ü©
  have h1 : MulEquiv.ulift ((limit.œÄ (autGaloisSystem' F) ‚ü®A‚ü©) f) =
    MulEquiv.ulift ((limit.œÄ (autGaloisSystem' F) ‚ü®A‚ü©) g) := h ‚ü®A‚ü©
  exact (EquivLike.injective _) h1

lemma autGalois_ext' (x y : autGalois F)
    (h : ‚àÄ (A : (Idx F)·µí·µñ), limit.œÄ (autGaloisSystem' F) A x = limit.œÄ (autGaloisSystem' F) A y) : x = y :=
  Concrete.limit_ext (autGaloisSystem' F) x y h

instance : HasColimit ((can F).rightOp ‚ãô coyoneda) where
  exists_colimit := ‚ü®cocone F, iscolimit F‚ü©

noncomputable def prorep : colimit ((can F).rightOp ‚ãô coyoneda) ‚âÖ F ‚ãô FintypeCat.incl :=
  colimit.isoColimitCocone ‚ü®cocone F, iscolimit F‚ü©

local notation "F'" => F ‚ãô FintypeCat.incl

noncomputable def iso0 : End F ‚âÖ End (F ‚ãô FintypeCat.incl) :=
  Equiv.toIso (NatTrans.equivOfCompFullyFaithful FintypeCat.incl)

noncomputable def iso1 : End F' ‚âÖ (colimit ((can F).rightOp ‚ãô coyoneda) ‚ü∂ F') :=
  (yoneda.obj (F ‚ãô FintypeCat.incl)).mapIso (prorep F).op

-- coproyoneda lemma
noncomputable def iso2 :
    (colimit ((can F).rightOp ‚ãô coyoneda) ‚ü∂ F') ‚âÖ limit (can F ‚ãô F' ‚ãô uliftFunctor.{u‚ÇÅ}) :=
  procoyonedaIso (can F) F'

noncomputable def iso3 : End F ‚âÖ limit (can F ‚ãô F' ‚ãô uliftFunctor.{u‚ÇÅ}) := by
  apply Iso.trans
  exact iso0 F
  apply Iso.trans (iso1 F) (iso2 F)

theorem iso3_pi (f : End F) (A : Idx F) :
    limit.œÄ (can F ‚ãô F' ‚ãô uliftFunctor.{u‚ÇÅ}) ‚ü®A‚ü© ((iso3 F).hom f) = ‚ü®f.app A.1 (A.2)‚ü© := by
  apply ULift.ext
  simp [iso3, iso2, iso1, iso0, iso1, iso0, prorep]
  change ((NatTrans.equivOfCompFullyFaithful FintypeCat.incl) f).app A.1
    (((colimit.Œπ ((can F).rightOp.comp coyoneda) A) ‚â´ (colimit.isoColimitCocone ‚ü®cocone F, iscolimit F‚ü©).hom).app
      (A.1) _) = f.app A.fst A.snd
  simp

noncomputable def galautiso' :
    autGaloisSystem' F ‚ãô forget GroupCat.{max u‚ÇÅ u‚ÇÇ} ‚âÖ can F ‚ãô F' ‚ãô uliftFunctor.{u‚ÇÅ} := by
  fapply NatIso.ofComponents
  ¬∑ intro ‚ü®‚ü®‚ü®A, (hgal : IsGalois A)‚ü©,a‚ü©‚ü©
    apply Equiv.toIso
    exact (Equiv.ulift.{u‚ÇÅ, u‚ÇÇ}).trans
      ((evaluationEquivOfIsGalois F A a).trans Equiv.ulift.{u‚ÇÅ, u‚ÇÇ}.symm)
  ¬∑ intro ‚ü®‚ü®‚ü®A, (_ : IsGalois A)‚ü©, a‚ü©‚ü© ‚ü®‚ü®‚ü®B, (_ : IsGalois B)‚ü©, b‚ü©‚ü© ‚ü®(f : A ‚ü∂ B), hf‚ü©
    dsimp
    ext ‚ü®œÜ : Aut A‚ü©
    apply ULift.ext
    erw [Equiv.ulift_apply, Equiv.ulift_apply]
    simp only [types_comp_apply, autGaloisSystem'_map, Function.comp_apply, uliftFunctor_map,
      FintypeCat.incl_map]
    erw [Equiv.ulift_symm_down, Equiv.ulift_symm_down]
    simp only [autMapMul, MonoidHom.mk'_apply, autMap, map_comp, FintypeCat.comp_apply]
    erw [Equiv.apply_symm_apply]
    rfl

/-- functorial isomorphism `F.obj A ‚âÉ Aut A` for Galois object `A`. -/
noncomputable def galautiso :
    can F ‚ãô F' ‚ãô uliftFunctor.{u‚ÇÅ} ‚âÖ autGaloisSystem' F ‚ãô forget GroupCat.{max u‚ÇÅ u‚ÇÇ} :=
  (galautiso' F).symm

@[simp]
theorem galautiso_app (A : Idx F) (b : F.obj A.1) :
    (galautiso F).hom.app (Opposite.op A) ‚ü®b‚ü© =
      ‚ü®(evaluationEquivOfIsGalois F A.1 A.2).symm b‚ü© :=
  rfl

noncomputable def iso4 : End F ‚âÖ limit (autGaloisSystem' F ‚ãô forget GroupCat.{max u‚ÇÅ u‚ÇÇ}) := by
  apply Iso.trans
  exact iso3 F
  exact lim.mapIso (galautiso F)

@[simp]
theorem iso4_pi_apply (A : Idx F) (f : End F) :
    limit.œÄ (autGaloisSystem' F ‚ãô forget _) (Opposite.op A) ((iso4 F).hom f) =
      ‚ü®(evaluationEquivOfIsGalois F A.fst A.snd).symm (f.app A.fst A.snd)‚ü© := by
  simp [iso4]
  erw [iso3_pi]
  rw [galautiso_app]

noncomputable def iso5' : End F ‚âÖ autGalois F := by
  show End F ‚âÖ (forget GroupCat.{max u‚ÇÅ u‚ÇÇ}).obj (limit (autGaloisSystem' F))
  apply Iso.trans
  exact iso4 F
  exact (preservesLimitIso (forget GroupCat.{max u‚ÇÅ u‚ÇÇ}) (autGaloisSystem' F)).symm

noncomputable def iso5 : End F ‚âÉ autGalois F := (iso5' F).toEquiv

lemma iso5_pi_foo (f : End F) (A : Idx F) :
    F.map (autGalois.œÄ F A (iso5 F f)).hom A.2 = f.app A.1 A.2 := by
  simp [iso5, iso5']
  rw [autGalois.œÄ_apply]
  change F.map
    (((iso4 F).hom
        ‚â´ (preservesLimitIso (forget GroupCat) (autGaloisSystem' F)).inv
        ‚â´ (forget GroupCat).map (limit.œÄ (autGaloisSystem' F) ‚ü®A‚ü©)) f).down.hom
    A.2 = _
  rw [preservesLimitsIso_inv_œÄ]
  simp

@[simp]
theorem iso5_mul (f g : End F) : (iso5 F) (g ‚â´ f) = (iso5 F g) * (iso5 F f) := by
  apply autGalois_ext F
  intro ‚ü®‚ü®‚ü®A, (_ : IsGalois A)‚ü©, a‚ü©‚ü©
  simp
  apply evaluation_aut_injective_of_isConnected F A a
  simp
  rw [iso5_pi_foo]
  simp
  rw [Aut.Aut_mul_def]
  simp [-FintypeCat.comp_apply]
  simp
  rw [iso5_pi_foo]
  change f.app A (g.app A a) =
    (f.app A ‚â´ F.map ((autGalois.œÄ F ‚ü®‚ü®A, _‚ü©, a‚ü©) ((iso5 F) g)).hom) a
  rw [‚Üê f.naturality]
  simp
  rw [iso5_pi_foo]

noncomputable def iso5Monoid : End F ‚âÉ* (autGalois F)·µê·µí·µñ :=
  MulEquiv.mk (Equiv.trans (iso5 F) MulOpposite.opEquiv) (by simp)

lemma iso5Monoid_pi (f : End F) (A : Idx F) :
    F.map (autGalois.œÄ F A (iso5Monoid F f).unop).hom A.2 = f.app A.1 A.2 :=
  iso5_pi_foo F f A

theorem FibreFunctor.end_isUnit (f : End F) : IsUnit f :=
  isUnit_of_equiv _ _ (iso5Monoid F).symm f

theorem FibreFunctor.end_isIso (f : End F) : IsIso f := by
  rw [‚Üê isUnit_iff_isIso]
  exact FibreFunctor.end_isUnit F f

noncomputable def autMulEquivAutGalois : Aut F ‚âÉ* (autGalois F)·µê·µí·µñ where
  toFun := MonoidHom.comp (iso5Monoid F) (Aut.toEnd F)
  invFun t := by
    let s : End F := (iso5Monoid F).symm t
    have : IsIso s := FibreFunctor.end_isIso F s
    apply asIso s
  left_inv t := by
    simp
    exact Aut.ext rfl
  right_inv t := by
    simp
    exact (MulEquiv.eq_symm_apply (iso5Monoid F)).mp rfl
  map_mul' := by simp

theorem autMulEquivAutGalois_œÄ (f : Aut F) (A : C) [inst : IsGalois A] (a : F.obj A) :
    F.map (autGalois.œÄ F ‚ü®‚ü®A, inst‚ü©, a‚ü© (autMulEquivAutGalois F f).unop).hom a = f.hom.app A a := by
  simp [autMulEquivAutGalois, iso5Monoid]
  rw [iso5_pi_foo]
  rfl

@[simp]
theorem autMulEquivAutGalois_symm_app (x : autGalois F) (A : C) [IsGalois A] (a : F.obj A) :
    ((autMulEquivAutGalois F).symm ‚ü®x‚ü©).hom.app A a =
      F.map (autGalois.œÄ F ‚ü®‚ü®A, (inferInstance : IsGalois A)‚ü©, a‚ü© x).hom a := by
  rw [‚Üê autMulEquivAutGalois_œÄ, MulEquiv.apply_symm_apply]
  rfl

lemma proj_surj (A : C) [inst : IsGalois A] (a : F.obj A) :
    Function.Surjective (autGalois.œÄ F ‚ü®‚ü®A, inst‚ü©, a‚ü©) := fun (œÉ : Aut A) ‚Ü¶ by
  have (i : (Idx F)·µí·µñ) : Finite ((autGaloisSystem' F ‚ãô forget _).obj i) :=
    inferInstanceAs <| Finite (ULift (Aut (i.1.1.1)))
  have fsur (i j : (Idx F)·µí·µñ) (f : i ‚ü∂ j) : Function.Surjective ((autGaloisSystem' F).map f) := by
    intro ‚ü®(œÜ : Aut j.1.1.1)‚ü©
    obtain ‚ü®œà, hœà‚ü© := autMap_surjective F f.1.1 i.1.2 j.1.2 œÜ
    use ‚ü®œà‚ü©
    simp only [autGaloisSystem'_map]
    apply ULift.ext
    exact hœà
  have := eval_section_surjective_of_surjective (autGaloisSystem' F ‚ãô forget _) fsur
  obtain ‚ü®s', hs‚ü© := eval_section_surjective_of_surjective
    (autGaloisSystem' F ‚ãô forget _) fsur ‚ü®‚ü®‚ü®A, inst‚ü©, a‚ü©‚ü© ‚ü®œÉ‚ü©
  simp only [comp_obj] at hs
  let s : limit _ := (Types.limitEquivSections (autGaloisSystem' F ‚ãô forget _)).symm s'
  let t : autGalois F := (preservesLimitIso (forget GroupCat) (autGaloisSystem' F)).inv s
  use t
  simp [t, s, autGalois.œÄ]
  change MulEquiv.ulift
      (((preservesLimitIso (forget GroupCat) (autGaloisSystem' F)).inv
        ‚â´ (forget _).map (limit.œÄ (autGaloisSystem' F) { unop := ‚ü®‚ü®A, inst‚ü©, a‚ü© }))
        ((Types.limitEquivSections ((autGaloisSystem' F).comp (forget GroupCat))).symm s')) =
    œÉ
  rw [preservesLimitsIso_inv_œÄ]
  simp
  rw [hs]
  rfl

instance (X : C) : MulAction (Aut F) (F.obj X) where
  smul œÉ x := œÉ.hom.app X x
  one_smul _ := rfl
  mul_smul _ _ _ := rfl

theorem mulAction_def {X : C} (œÉ : Aut F) (x : F.obj X) :
    œÉ ‚Ä¢ x = œÉ.hom.app X x :=
  rfl

theorem pretransitive_of_galois (X : C) [IsGalois X] :
    MulAction.IsPretransitive (Aut F) (F.obj X) := by
  refine ‚ü®fun x y ‚Ü¶ ?_‚ü©
  obtain ‚ü®(œÜ : Aut X), h‚ü© := MulAction.IsPretransitive.exists_smul_eq (M := Aut X) x y
  obtain ‚ü®a, ha‚ü© := proj_surj F X x œÜ
  use (autMulEquivAutGalois F).symm ‚ü®a‚ü©
  simpa [mulAction_def, ha]

instance pretransitive_of_isConnected (X : C) [IsConnected X] :
    MulAction.IsPretransitive (Aut F) (F.obj X) := by
  obtain ‚ü®A, f, hgal‚ü© := exists_hom_from_galois_of_connected F X
  have hs : Function.Surjective (F.map f) := surjective_of_nonempty_fiber_of_isConnected F f
  refine ‚ü®fun x y ‚Ü¶ ?_‚ü©
  obtain ‚ü®a, ha‚ü© := hs x
  obtain ‚ü®b, hb‚ü© := hs y
  have : MulAction.IsPretransitive (Aut F) (F.obj A) := pretransitive_of_galois F A
  obtain ‚ü®œÉ, (hœÉ : œÉ.hom.app A a = b)‚ü© := MulAction.exists_smul_eq (Aut F) a b
  use œÉ
  rw [‚Üêha, ‚Üêhb]
  show (F.map f ‚â´ œÉ.hom.app X) a = F.map f b
  rw [œÉ.hom.naturality, FintypeCat.comp_apply, hœÉ]
