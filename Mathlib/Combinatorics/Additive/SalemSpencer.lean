/-
Copyright (c) 2021 Ya√´l Dillies, Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ya√´l Dillies, Bhavik Mehta
-/
import Mathlib.Analysis.Asymptotics.Asymptotics
import Mathlib.Analysis.Convex.StrictConvexSpace
import Mathlib.Combinatorics.Additive.FreimanHom

#align_import combinatorics.additive.salem_spencer from "leanprover-community/mathlib"@"acf5258c81d0bc7cb254ed026c1352e685df306c"

/-!
# Salem-Spencer sets and Roth numbers

This file defines Salem-Spencer sets and the Roth number of a set.

A Salem-Spencer set is a set without arithmetic progressions of length `3`. Equivalently, the
average of any two distinct elements is not in the set.

The Roth number of a finset is the size of its biggest Salem-Spencer subset. This is a more general
definition than the one often found in mathematical literature, where the `n`-th Roth number is
the size of the biggest Salem-Spencer subset of `{0, ..., n - 1}`.

## Main declarations

* `MulSalemSpencer`: Predicate for a set to be multiplicative Salem-Spencer.
* `AddSalemSpencer`: Predicate for a set to be additive Salem-Spencer.
* `mulRothNumber`: The multiplicative Roth number of a finset.
* `addRothNumber`: The additive Roth number of a finset.
* `rothNumberNat`: The Roth number of a natural. This corresponds to
  `addRothNumber (Finset.range n)`.

## TODO

* Can `addSalemSpencer_iff_eq_right` be made more general?
* Generalize `MulSalemSpencer.image` to Freiman homs

## Tags

Salem-Spencer, Roth, arithmetic progression, average, three-free
-/


open Finset Function Metric Nat

open Pointwise

variable {F Œ± Œ≤ ùïú E : Type*}

section SalemSpencer

open Set

section Monoid

variable [Monoid Œ±] [Monoid Œ≤] (s t : Set Œ±)

/-- A multiplicative Salem-Spencer, aka non averaging, set `s` in a monoid is a set such that the
multiplicative average of any two distinct elements is not in the set. -/
@[to_additive "A Salem-Spencer, aka non averaging, set `s` in an additive monoid
is a set such that the average of any two distinct elements is not in the set."]
def MulSalemSpencer : Prop :=
  ‚àÄ ‚¶Éa‚¶Ñ, a ‚àà s ‚Üí ‚àÄ ‚¶Éb‚¶Ñ, b ‚àà s ‚Üí ‚àÄ ‚¶Éc‚¶Ñ, c ‚àà s ‚Üí a * c = b * b ‚Üí a = b
#align mul_salem_spencer MulSalemSpencer
#align add_salem_spencer AddSalemSpencer

/-- Whether a given finset is Salem-Spencer is decidable. -/
@[to_additive "Whether a given finset is Salem-Spencer is decidable."]
instance {Œ± : Type*} [DecidableEq Œ±] [Monoid Œ±] {s : Finset Œ±} :
    Decidable (MulSalemSpencer (s : Set Œ±)) :=
  decidable_of_iff (‚àÄ a ‚àà s, ‚àÄ b ‚àà s, ‚àÄ c ‚àà s, a * c = b * b ‚Üí a = b) Iff.rfl

variable {s t}

@[to_additive]
theorem MulSalemSpencer.mono (h : t ‚äÜ s) (hs : MulSalemSpencer s) : MulSalemSpencer t :=
  fun _ ha _ hb _ hc ‚Ü¶ hs (h ha) (h hb) (h hc)
#align mul_salem_spencer.mono MulSalemSpencer.mono
#align add_salem_spencer.mono AddSalemSpencer.mono

@[to_additive (attr := simp)]
theorem mulSalemSpencer_empty : MulSalemSpencer (‚àÖ : Set Œ±) := fun _ _ _ ha => ha.elim
#align mul_salem_spencer_empty mulSalemSpencer_empty
#align add_salem_spencer_empty addSalemSpencer_empty

@[to_additive]
theorem Set.Subsingleton.mulSalemSpencer (hs : s.Subsingleton) : MulSalemSpencer s :=
  fun _ ha _ hb _ _ _ ‚Ü¶ hs ha hb
#align set.subsingleton.mul_salem_spencer Set.Subsingleton.mulSalemSpencer
#align set.subsingleton.add_salem_spencer Set.Subsingleton.addSalemSpencer

@[to_additive (attr := simp)]
theorem mulSalemSpencer_singleton (a : Œ±) : MulSalemSpencer ({a} : Set Œ±) :=
  subsingleton_singleton.mulSalemSpencer
#align mul_salem_spencer_singleton mulSalemSpencer_singleton
#align add_salem_spencer_singleton addSalemSpencer_singleton

@[to_additive AddSalemSpencer.prod]
theorem MulSalemSpencer.prod {t : Set Œ≤} (hs : MulSalemSpencer s) (ht : MulSalemSpencer t) :
    MulSalemSpencer (s √óÀ¢ t) := fun _ ha _ hb _ hc h ‚Ü¶
  Prod.ext (hs ha.1 hb.1 hc.1 (Prod.ext_iff.1 h).1) (ht ha.2 hb.2 hc.2 (Prod.ext_iff.1 h).2)
#align mul_salem_spencer.prod MulSalemSpencer.prod
#align add_salem_spencer.prod AddSalemSpencer.prod

@[to_additive]
theorem mulSalemSpencer_pi {Œπ : Type*} {Œ± : Œπ ‚Üí Type*} [‚àÄ i, Monoid (Œ± i)] {s : ‚àÄ i, Set (Œ± i)}
    (hs : ‚àÄ i, MulSalemSpencer (s i)) : MulSalemSpencer ((univ : Set Œπ).pi s) :=
  fun _ ha _ hb _ hc h ‚Ü¶
  funext fun i => hs i (ha i trivial) (hb i trivial) (hc i trivial) <| congr_fun h i
#align mul_salem_spencer_pi mulSalemSpencer_pi
#align add_salem_spencer_pi addSalemSpencer_pi

end Monoid

section CommMonoid
variable [CommMonoid Œ±] [CommMonoid Œ≤] {s : Set Œ±} {t : Set Œ≤} {f : Œ± ‚Üí Œ≤} {a : Œ±}

/-- Arithmetic progressions of length three are preserved under `2`-Freiman homomorphisms. --/
@[to_additive]
lemma IsMulFreimanHom.mulSalemSpencer (hf : IsMulFreimanHom 2 s t f) (hf' : s.InjOn f)
    (ht : MulSalemSpencer t) : MulSalemSpencer s :=
  fun _ ha _ hb _ hc habc ‚Ü¶ hf' ha hb <| ht (hf.mapsTo ha) (hf.mapsTo hb) (hf.mapsTo hc) <|
    hf.mul_eq_mul ha hc hb hb habc
#align mul_salem_spencer.of_image IsMulFreimanHom.mulSalemSpencer
#align add_salem_spencer.of_image IsAddFreimanHom.addSalemSpencer

/-- Arithmetic progressions of length three are preserved under `2`-Freiman isomorphisms. --/
@[to_additive]
lemma IsMulFreimanIso.mulSalemSpencer_congr (hf : IsMulFreimanIso 2 s t f) :
    MulSalemSpencer s ‚Üî MulSalemSpencer t where
  mpr := hf.isMulFreimanHom.mulSalemSpencer hf.bijOn.injOn
  mp hs a hfa b hfb c hfc habc := by
    obtain ‚ü®a, ha, rfl‚ü© := hf.bijOn.surjOn hfa
    obtain ‚ü®b, hb, rfl‚ü© := hf.bijOn.surjOn hfb
    obtain ‚ü®c, hc, rfl‚ü© := hf.bijOn.surjOn hfc
    exact congr_arg f $ hs ha hb hc $ (hf.mul_eq_mul ha hc hb hb).1 habc

@[to_additive]
theorem MulSalemSpencer.image [FunLike F Œ± Œ≤] [MulHomClass F Œ± Œ≤] (f : F) (hf : (s * s).InjOn f)
    (h : MulSalemSpencer s) : MulSalemSpencer (f '' s) := by
  rintro _ ‚ü®a, ha, rfl‚ü© _ ‚ü®b, hb, rfl‚ü© _ ‚ü®c, hc, rfl‚ü© habc
  rw [h ha hb hc (hf (mul_mem_mul ha hc) (mul_mem_mul hb hb) <| by rwa [map_mul, map_mul])]
#align mul_salem_spencer.image MulSalemSpencer.image
#align add_salem_spencer.image AddSalemSpencer.image

end CommMonoid

section CancelCommMonoid

variable [CancelCommMonoid Œ±] {s : Set Œ±} {a : Œ±}

lemma MulSalemSpencer.eq_right (hs : MulSalemSpencer s) :
    ‚àÄ ‚¶Éa‚¶Ñ, a ‚àà s ‚Üí ‚àÄ ‚¶Éb‚¶Ñ, b ‚àà s ‚Üí ‚àÄ ‚¶Éc‚¶Ñ, c ‚àà s ‚Üí a * c = b * b ‚Üí b = c := by
  rintro a ha b hb c hc habc
  obtain rfl := hs ha hb hc habc
  simpa using habc.symm

@[to_additive] lemma mulSalemSpencer_insert :
    MulSalemSpencer (insert a s) ‚Üî MulSalemSpencer s ‚àß
      (‚àÄ ‚¶Éb‚¶Ñ, b ‚àà s ‚Üí ‚àÄ ‚¶Éc‚¶Ñ, c ‚àà s ‚Üí a * c = b * b ‚Üí a = b) ‚àß
        ‚àÄ ‚¶Éb‚¶Ñ, b ‚àà s ‚Üí ‚àÄ ‚¶Éc‚¶Ñ, c ‚àà s ‚Üí b * c = a * a ‚Üí b = a := by
  refine ‚ü®fun hs ‚Ü¶ ‚ü®hs.mono (subset_insert _ _),
    fun b hb c hc ‚Ü¶ hs (Or.inl rfl) (Or.inr hb) (Or.inr hc),
    fun b hb c hc ‚Ü¶ hs (Or.inr hb) (Or.inl rfl) (Or.inr hc)‚ü©, ?_‚ü©
  rintro ‚ü®hs, ha, ha'‚ü© b hb c hc d hd h
  rw [mem_insert_iff] at hb hc hd
  obtain rfl | hb := hb <;> obtain rfl | hc := hc
  ¬∑ rfl
  all_goals obtain rfl | hd := hd
  ¬∑ exact (ha' hc hc h.symm).symm
  ¬∑ exact ha hc hd h
  ¬∑ exact mul_right_cancel h
  ¬∑ exact ha' hb hd h
  ¬∑ obtain rfl := ha hc hb ((mul_comm _ _).trans h)
    exact ha' hb hc h
  ¬∑ exact hs hb hc hd h
#align mul_salem_spencer_insert mulSalemSpencer_insert
#align add_salem_spencer_insert addSalemSpencer_insert

@[to_additive]
theorem MulSalemSpencer.smul_set (hs : MulSalemSpencer s) : MulSalemSpencer (a ‚Ä¢ s) := by
  rintro _ ‚ü®b, hb, rfl‚ü© _ ‚ü®c, hc, rfl‚ü© _ ‚ü®d, hd, rfl‚ü© h
  exact congr_arg (a ‚Ä¢ ¬∑) $ hs hb hc hd $ by simpa [mul_mul_mul_comm _ _ a] using h
#align mul_salem_spencer.mul_left MulSalemSpencer.smul_set
#align add_salem_spencer.add_left AddSalemSpencer.vadd_set

#noalign mul_salem_spencer.mul_right
#noalign add_salem_spencer.add_right

@[to_additive]
theorem mulSalemSpencer_smul_set : MulSalemSpencer ((a * ¬∑) '' s) ‚Üî MulSalemSpencer s :=
  ‚ü®fun hs b hb c hc d hd h ‚Ü¶
    mul_left_cancel
      (hs (mem_image_of_mem _ hb) (mem_image_of_mem _ hc) (mem_image_of_mem _ hd) <| by
        rw [mul_mul_mul_comm, h, mul_mul_mul_comm]),
    MulSalemSpencer.smul_set‚ü©
#align mul_salem_spencer_mul_left_iff mulSalemSpencer_smul_set
#align add_salem_spencer_add_left_iff addSalemSpencer_vadd_set

#noalign mul_salem_spencer_mul_right_iff
#noalign add_salem_spencer_add_right_iff

end CancelCommMonoid

section OrderedCancelCommMonoid

variable [OrderedCancelCommMonoid Œ±] {s : Set Œ±} {a : Œ±}

@[to_additive]
theorem mulSalemSpencer_insert_of_lt (hs : ‚àÄ i ‚àà s, i < a) :
    MulSalemSpencer (insert a s) ‚Üî
      MulSalemSpencer s ‚àß ‚àÄ ‚¶Éb‚¶Ñ, b ‚àà s ‚Üí ‚àÄ ‚¶Éc‚¶Ñ, c ‚àà s ‚Üí a * c = b * b ‚Üí a = b := by
  refine' mulSalemSpencer_insert.trans _
  rw [‚Üê and_assoc]
  exact and_iff_left fun b hb c hc h => ((mul_lt_mul_of_lt_of_lt (hs _ hb) (hs _ hc)).ne h).elim
#align mul_salem_spencer_insert_of_lt mulSalemSpencer_insert_of_lt
#align add_salem_spencer_insert_of_lt addSalemSpencer_insert_of_lt

end OrderedCancelCommMonoid

section CancelCommMonoidWithZero

variable [CancelCommMonoidWithZero Œ±] [NoZeroDivisors Œ±] {s : Set Œ±} {a : Œ±}

theorem MulSalemSpencer.smul_set‚ÇÄ (hs : MulSalemSpencer s) (ha : a ‚â† 0) :
    MulSalemSpencer ((a * ¬∑) '' s) := by
  rintro _ ‚ü®b, hb, rfl‚ü© _ ‚ü®c, hc, rfl‚ü© _ ‚ü®d, hd, rfl‚ü© h
  exact congr_arg (a ‚Ä¢ ¬∑) $ hs hb hc hd $ by simpa [mul_mul_mul_comm _ _ a, ha] using h
#align mul_salem_spencer.mul_left‚ÇÄ MulSalemSpencer.smul_set‚ÇÄ

#noalign mul_salem_spencer.mul_right‚ÇÄ.mul_right‚ÇÄ

theorem mulSalemSpencer_smul_set‚ÇÄ (ha : a ‚â† 0) : MulSalemSpencer (a ‚Ä¢ s) ‚Üî MulSalemSpencer s :=
  ‚ü®fun hs b hb c hc d hd h ‚Ü¶
    mul_left_cancel‚ÇÄ ha
      (hs (Set.mem_image_of_mem _ hb) (Set.mem_image_of_mem _ hc) (Set.mem_image_of_mem _ hd) <| by
        rw [smul_eq_mul, smul_eq_mul, mul_mul_mul_comm, h, mul_mul_mul_comm]),
    fun hs => hs.smul_set‚ÇÄ ha‚ü©
#align mul_salem_spencer_mul_left_iff‚ÇÄ mulSalemSpencer_smul_set‚ÇÄ

#noalign mul_salem_spencer_mul_right_iff‚ÇÄ

end CancelCommMonoidWithZero

section Nat

theorem addSalemSpencer_iff_eq_right {s : Set ‚Ñï} :
    AddSalemSpencer s ‚Üî ‚àÄ ‚¶Éa‚¶Ñ, a ‚àà s ‚Üí ‚àÄ ‚¶Éb‚¶Ñ, b ‚àà s ‚Üí ‚àÄ ‚¶Éc‚¶Ñ, c ‚àà s ‚Üí a + c = b + b ‚Üí a = c := by
  refine' forall‚ÇÑ_congr fun a _ha b hb => forall‚ÇÉ_congr fun c hc habc => ‚ü®_, _‚ü©
  ¬∑ rintro rfl
    exact (add_left_cancel habc).symm
  ¬∑ rintro rfl
    simp_rw [‚Üê two_mul] at habc
    exact mul_left_cancel‚ÇÄ two_ne_zero habc
#align add_salem_spencer_iff_eq_right addSalemSpencer_iff_eq_right

end Nat

/-- The frontier of a closed strictly convex set only contains trivial arithmetic progressions.
The idea is that an arithmetic progression is contained on a line and the frontier of a strictly
convex set does not contain lines. -/
theorem addSalemSpencer_frontier [LinearOrderedField ùïú] [TopologicalSpace E] [AddCommMonoid E]
    [Module ùïú E] {s : Set E} (hs‚ÇÄ : IsClosed s) (hs‚ÇÅ : StrictConvex ùïú s) :
    AddSalemSpencer (frontier s) := by
  intro a ha b hb c hc habc
  obtain rfl : (1 / 2 : ùïú) ‚Ä¢ a + (1 / 2 : ùïú) ‚Ä¢ c = b := by
    rwa [‚Üê smul_add, one_div, inv_smul_eq_iff‚ÇÄ (show (2 : ùïú) ‚â† 0 by norm_num), two_smul]
  have :=
    hs‚ÇÅ.eq (hs‚ÇÄ.frontier_subset ha) (hs‚ÇÄ.frontier_subset hc) one_half_pos one_half_pos
      (add_halves _) hb.2
  simp [this, ‚Üê add_smul]
  ring_nf
  simp
#align add_salem_spencer_frontier addSalemSpencer_frontier

theorem addSalemSpencer_sphere [NormedAddCommGroup E] [NormedSpace ‚Ñù E] [StrictConvexSpace ‚Ñù E]
    (x : E) (r : ‚Ñù) : AddSalemSpencer (sphere x r) := by
  obtain rfl | hr := eq_or_ne r 0
  ¬∑ rw [sphere_zero]
    exact addSalemSpencer_singleton _
  ¬∑ convert addSalemSpencer_frontier isClosed_ball (strictConvex_closedBall ‚Ñù x r)
    exact (frontier_closedBall _ hr).symm
#align add_salem_spencer_sphere addSalemSpencer_sphere

end SalemSpencer

open Finset

section RothNumber

variable [DecidableEq Œ±]

section Monoid

variable [Monoid Œ±] [DecidableEq Œ≤] [Monoid Œ≤] (s t : Finset Œ±)

/-- The multiplicative Roth number of a finset is the cardinality of its biggest multiplicative
Salem-Spencer subset. -/
@[to_additive "The additive Roth number of a finset is the cardinality of its biggest additive
Salem-Spencer subset. The usual Roth number corresponds to `addRothNumber (Finset.range n)`, see
`rothNumberNat`. "]
def mulRothNumber : Finset Œ± ‚Üío ‚Ñï :=
  ‚ü®fun s ‚Ü¶ Nat.findGreatest (fun m ‚Ü¶ ‚àÉ t ‚äÜ s, t.card = m ‚àß MulSalemSpencer (t : Set Œ±)) s.card, by
    rintro t u htu
    refine' Nat.findGreatest_mono (fun m => _) (card_le_card htu)
    rintro ‚ü®v, hvt, hv‚ü©
    exact ‚ü®v, hvt.trans htu, hv‚ü©‚ü©
#align mul_roth_number mulRothNumber
#align add_roth_number addRothNumber

@[to_additive]
theorem mulRothNumber_le : mulRothNumber s ‚â§ s.card := Nat.findGreatest_le s.card
#align mul_roth_number_le mulRothNumber_le
#align add_roth_number_le addRothNumber_le

@[to_additive]
theorem mulRothNumber_spec :
    ‚àÉ t ‚äÜ s, t.card = mulRothNumber s ‚àß MulSalemSpencer (t : Set Œ±) :=
  Nat.findGreatest_spec (P := fun m ‚Ü¶ ‚àÉ t ‚äÜ s, t.card = m ‚àß MulSalemSpencer (t : Set Œ±))
    (Nat.zero_le _) ‚ü®‚àÖ, empty_subset _, card_empty, by norm_cast; exact mulSalemSpencer_empty‚ü©
#align mul_roth_number_spec mulRothNumber_spec
#align add_roth_number_spec addRothNumber_spec

variable {s t} {n : ‚Ñï}

@[to_additive]
theorem MulSalemSpencer.le_mulRothNumber (hs : MulSalemSpencer (s : Set Œ±)) (h : s ‚äÜ t) :
    s.card ‚â§ mulRothNumber t :=
  le_findGreatest (card_le_card h) ‚ü®s, h, rfl, hs‚ü©
#align mul_salem_spencer.le_mul_roth_number MulSalemSpencer.le_mulRothNumber
#align add_salem_spencer.le_add_roth_number AddSalemSpencer.le_addRothNumber

@[to_additive]
theorem MulSalemSpencer.mulRothNumber_eq (hs : MulSalemSpencer (s : Set Œ±)) :
    mulRothNumber s = s.card :=
  (mulRothNumber_le _).antisymm <| hs.le_mulRothNumber <| Subset.refl _
#align mul_salem_spencer.roth_number_eq MulSalemSpencer.mulRothNumber_eq
#align add_salem_spencer.roth_number_eq AddSalemSpencer.addRothNumber_eq

@[to_additive (attr := simp)]
theorem mulRothNumber_empty : mulRothNumber (‚àÖ : Finset Œ±) = 0 :=
  Nat.eq_zero_of_le_zero <| (mulRothNumber_le _).trans card_empty.le
#align mul_roth_number_empty mulRothNumber_empty
#align add_roth_number_empty addRothNumber_empty

@[to_additive (attr := simp)]
theorem mulRothNumber_singleton (a : Œ±) : mulRothNumber ({a} : Finset Œ±) = 1 := by
  refine' MulSalemSpencer.mulRothNumber_eq _
  rw [coe_singleton]
  exact mulSalemSpencer_singleton a
#align mul_roth_number_singleton mulRothNumber_singleton
#align add_roth_number_singleton addRothNumber_singleton

@[to_additive]
theorem mulRothNumber_union_le (s t : Finset Œ±) :
    mulRothNumber (s ‚à™ t) ‚â§ mulRothNumber s + mulRothNumber t :=
  let ‚ü®u, hus, hcard, hu‚ü© := mulRothNumber_spec (s ‚à™ t)
  calc
    mulRothNumber (s ‚à™ t) = u.card := hcard.symm
    _ = (u ‚à© s ‚à™ u ‚à© t).card := by rw [‚Üê inter_union_distrib_left, inter_eq_left.2 hus]
    _ ‚â§ (u ‚à© s).card + (u ‚à© t).card := card_union_le _ _
    _ ‚â§ mulRothNumber s + mulRothNumber t := _root_.add_le_add
      ((hu.mono <| inter_subset_left _ _).le_mulRothNumber <| inter_subset_right _ _)
      ((hu.mono <| inter_subset_left _ _).le_mulRothNumber <| inter_subset_right _ _)
#align mul_roth_number_union_le mulRothNumber_union_le
#align add_roth_number_union_le addRothNumber_union_le

@[to_additive]
theorem le_mulRothNumber_product (s : Finset Œ±) (t : Finset Œ≤) :
    mulRothNumber s * mulRothNumber t ‚â§ mulRothNumber (s √óÀ¢ t) := by
  obtain ‚ü®u, hus, hucard, hu‚ü© := mulRothNumber_spec s
  obtain ‚ü®v, hvt, hvcard, hv‚ü© := mulRothNumber_spec t
  rw [‚Üê hucard, ‚Üê hvcard, ‚Üê card_product]
  refine' MulSalemSpencer.le_mulRothNumber _ (product_subset_product hus hvt)
  rw [coe_product]
  exact hu.prod hv
#align le_mul_roth_number_product le_mulRothNumber_product
#align le_add_roth_number_product le_addRothNumber_product

@[to_additive]
theorem mulRothNumber_lt_of_forall_not_mulSalemSpencer
    (h : ‚àÄ t ‚àà powersetCard n s, ¬¨MulSalemSpencer ((t : Finset Œ±) : Set Œ±)) :
    mulRothNumber s < n := by
  obtain ‚ü®t, hts, hcard, ht‚ü© := mulRothNumber_spec s
  rw [‚Üê hcard, ‚Üê not_le]
  intro hn
  obtain ‚ü®u, hut, rfl‚ü© := exists_smaller_set t n hn
  exact h _ (mem_powersetCard.2 ‚ü®hut.trans hts, rfl‚ü©) (ht.mono hut)
#align mul_roth_number_lt_of_forall_not_mul_salem_spencer mulRothNumber_lt_of_forall_not_mulSalemSpencer
#align add_roth_number_lt_of_forall_not_add_salem_spencer addRothNumber_lt_of_forall_not_addSalemSpencer

end Monoid

section CommMonoid
variable [CommMonoid Œ±] [CommMonoid Œ≤] [DecidableEq Œ≤] {A : Finset Œ±} {B : Finset Œ≤} {f : Œ± ‚Üí Œ≤}

/-- Arithmetic progressions can be pushed forward along bijective 2-Freiman homs. -/
@[to_additive]
lemma IsMulFreimanHom.mulRothNumber_mono (hf : IsMulFreimanHom 2 A B f) (hf' : Set.BijOn f A B) :
    mulRothNumber B ‚â§ mulRothNumber A := by
  obtain ‚ü®s, hsB, hcard, hs‚ü© := mulRothNumber_spec B
  have hsA : invFunOn f A '' s ‚äÜ A :=
    (hf'.surjOn.mapsTo_invFunOn.mono (coe_subset.2 hsB) Subset.rfl).image_subset
  have hfsA : Set.SurjOn f A s := hf'.surjOn.mono Subset.rfl (coe_subset.2 hsB)
  rw [‚Üê hcard, ‚Üê s.card_image_of_injOn ((invFunOn_injOn_image f _).mono hfsA)]
  refine MulSalemSpencer.le_mulRothNumber ?_ (mod_cast hsA)
  simpa using (hf.subset hsA hfsA.bijOn_subset.mapsTo).mulSalemSpencer (hf'.injOn.mono hsA) hs

/-- Arithmetic progressions are preserved under 2-Freiman isos. -/
@[to_additive]
lemma IsMulFreimanIso.mulRothNumber_congr (hf : IsMulFreimanIso 2 A B f) :
    mulRothNumber A = mulRothNumber B := by
  refine le_antisymm ?_ (hf.isMulFreimanHom.mulRothNumber_mono hf.bijOn)
  obtain ‚ü®s, hsA, hcard, hs‚ü© := mulRothNumber_spec A
  rw [‚Üê coe_subset] at hsA
  have hfs : Set.InjOn f s := hf.bijOn.injOn.mono hsA
  have := (hf.subset hsA hfs.bijOn_image).mulSalemSpencer_congr.1 hs
  rw [‚Üê coe_image] at this
  rw [‚Üê hcard, ‚Üê Finset.card_image_of_injOn hfs]
  refine this.le_mulRothNumber ?_
  rw [‚Üê coe_subset, coe_image]
  exact (hf.bijOn.mapsTo.mono hsA Subset.rfl).image_subset

end CommMonoid

section CancelCommMonoid

variable [CancelCommMonoid Œ±] (s : Finset Œ±) (a : Œ±)

@[to_additive (attr := simp)]
theorem mulRothNumber_map_mul_left :
    mulRothNumber (s.map <| mulLeftEmbedding a) = mulRothNumber s := by
  refine' le_antisymm _ _
  ¬∑ obtain ‚ü®u, hus, hcard, hu‚ü© := mulRothNumber_spec (s.map <| mulLeftEmbedding a)
    rw [subset_map_iff] at hus
    obtain ‚ü®u, hus, rfl‚ü© := hus
    rw [coe_map] at hu
    rw [‚Üê hcard, card_map]
    exact (mulSalemSpencer_smul_set.1 hu).le_mulRothNumber hus
  ¬∑ obtain ‚ü®u, hus, hcard, hu‚ü© := mulRothNumber_spec s
    have h : MulSalemSpencer (u.map <| mulLeftEmbedding a : Set Œ±) := by
      rw [coe_map]
      exact hu.smul_set
    convert h.le_mulRothNumber (map_subset_map.2 hus) using 1
    rw [card_map, hcard]
#align mul_roth_number_map_mul_left mulRothNumber_map_mul_left
#align add_roth_number_map_add_left addRothNumber_map_add_left

@[to_additive (attr := simp)]
theorem mulRothNumber_map_mul_right :
    mulRothNumber (s.map <| mulRightEmbedding a) = mulRothNumber s := by
  rw [‚Üê mulLeftEmbedding_eq_mulRightEmbedding, mulRothNumber_map_mul_left s a]
#align mul_roth_number_map_mul_right mulRothNumber_map_mul_right
#align add_roth_number_map_add_right addRothNumber_map_add_right

end CancelCommMonoid

end RothNumber

section rothNumberNat

variable {s : Finset ‚Ñï} {k n : ‚Ñï}

/-- The Roth number of a natural `N` is the largest integer `m` for which there is a subset of
`range N` of size `m` with no arithmetic progression of length 3.
Trivially, `rothNumberNat N ‚â§ N`, but Roth's theorem (proved in 1953) shows that
`rothNumberNat N = o(N)` and the construction by Behrend gives a lower bound of the form
`N * exp(-C sqrt(log(N))) ‚â§ rothNumberNat N`.
A significant refinement of Roth's theorem by Bloom and Sisask announced in 2020 gives
`rothNumberNat N = O(N / (log N)^(1+c))` for an absolute constant `c`. -/
def rothNumberNat : ‚Ñï ‚Üío ‚Ñï :=
  ‚ü®fun n => addRothNumber (range n), addRothNumber.mono.comp range_mono‚ü©
#align roth_number_nat rothNumberNat

theorem rothNumberNat_def (n : ‚Ñï) : rothNumberNat n = addRothNumber (range n) :=
  rfl
#align roth_number_nat_def rothNumberNat_def

theorem rothNumberNat_le (N : ‚Ñï) : rothNumberNat N ‚â§ N :=
  (addRothNumber_le _).trans (card_range _).le
#align roth_number_nat_le rothNumberNat_le

theorem rothNumberNat_spec (n : ‚Ñï) :
    ‚àÉ t ‚äÜ range n, t.card = rothNumberNat n ‚àß AddSalemSpencer (t : Set ‚Ñï) :=
  addRothNumber_spec _
#align roth_number_nat_spec rothNumberNat_spec

/-- A verbose specialization of `addSalemSpencer.le_addRothNumber`, sometimes convenient in
practice. -/
theorem AddSalemSpencer.le_rothNumberNat (s : Finset ‚Ñï) (hs : AddSalemSpencer (s : Set ‚Ñï))
    (hsn : ‚àÄ x ‚àà s, x < n) (hsk : s.card = k) : k ‚â§ rothNumberNat n :=
  hsk.ge.trans <| hs.le_addRothNumber fun x hx => mem_range.2 <| hsn x hx
#align add_salem_spencer.le_roth_number_nat AddSalemSpencer.le_rothNumberNat

/-- The Roth number is a subadditive function. Note that by Fekete's lemma this shows that
the limit `rothNumberNat N / N` exists, but Roth's theorem gives the stronger result that this
limit is actually `0`. -/
theorem rothNumberNat_add_le (M N : ‚Ñï) :
    rothNumberNat (M + N) ‚â§ rothNumberNat M + rothNumberNat N := by
  simp_rw [rothNumberNat_def]
  rw [range_add_eq_union, ‚Üê addRothNumber_map_add_left (range N) M]
  exact addRothNumber_union_le _ _
#align roth_number_nat_add_le rothNumberNat_add_le

@[simp]
theorem rothNumberNat_zero : rothNumberNat 0 = 0 :=
  rfl
#align roth_number_nat_zero rothNumberNat_zero

theorem addRothNumber_Ico (a b : ‚Ñï) : addRothNumber (Ico a b) = rothNumberNat (b - a) := by
  obtain h | h := le_total b a
  ¬∑ rw [tsub_eq_zero_of_le h, Ico_eq_empty_of_le h, rothNumberNat_zero, addRothNumber_empty]
  convert addRothNumber_map_add_left _ a
  rw [range_eq_Ico, map_eq_image]
  convert (image_add_left_Ico 0 (b - a) _).symm
  exact (add_tsub_cancel_of_le h).symm
#align add_roth_number_Ico addRothNumber_Ico

open Asymptotics Filter

theorem rothNumberNat_isBigOWith_id :
    IsBigOWith 1 atTop (fun N => (rothNumberNat N : ‚Ñù)) fun N => (N : ‚Ñù) :=
  isBigOWith_of_le _ <| by simpa only [Real.norm_natCast, Nat.cast_le] using rothNumberNat_le
set_option linter.uppercaseLean3 false in
#align roth_number_nat_is_O_with_id rothNumberNat_isBigOWith_id

/-- The Roth number has the trivial bound `rothNumberNat N = O(N)`. -/
theorem rothNumberNat_isBigO_id : (fun N => (rothNumberNat N : ‚Ñù)) =O[atTop] fun N => (N : ‚Ñù) :=
  rothNumberNat_isBigOWith_id.isBigO
set_option linter.uppercaseLean3 false in
#align roth_number_nat_is_O_id rothNumberNat_isBigO_id

lemma Fin.addRothNumber_eq_rothNumberNat (hkn : 2 * k ‚â§ n) :
    addRothNumber (Iio k : Finset (Fin n.succ)) = rothNumberNat k :=
  IsAddFreimanIso.addRothNumber_congr $ mod_cast isAddFreimanIso_Iio two_ne_zero hkn

lemma Fin.addRothNumber_le_rothNumberNat (k n : ‚Ñï) (hkn : k ‚â§ n) :
    addRothNumber (Iio k : Finset (Fin n.succ)) ‚â§ rothNumberNat k := by
  suffices h : Set.BijOn (Nat.cast : ‚Ñï ‚Üí Fin n.succ) (range k) (Iio k : Finset (Fin n.succ)) by
    exact (AddMonoidHomClass.isAddFreimanHom (Nat.castRingHom _) h.mapsTo).addRothNumber_mono h
  refine ‚ü®?_, (CharP.natCast_injOn_Iio _ n.succ).mono (by simp; omega), ?_‚ü©
  ¬∑ simpa using fun x ‚Ü¶ cast_strictMono hkn
  simp only [Set.SurjOn, coe_Iio, Set.subset_def, Set.mem_Iio, Set.mem_image, lt_iff_val_lt_val,
    val_cast_of_lt, Nat.lt_succ_iff.2 hkn, coe_range]
  exact fun x hx ‚Ü¶ ‚ü®x, hx, by simp‚ü©

end rothNumberNat
